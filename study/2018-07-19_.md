date: 2018-07-19 22:07:08
layout: post
title: Vim Tips
tags: ['vim', 'shell', 'highlight']

## Vimdiff

버퍼에서 자주 쓰는 기능입니다.
`diff` 내용을 IDE 비슷하게 보여주는 기능입니다.
간단하게 두 컨텐츠의 차이점을 빠르게 눈으로 확인하면서 비교할 수 있습니다.

`:h vimdiff`, `:h diffthis` 로 자세한 설명을 참고하실 수 있습니다.

```
vimdiff A.txt B.txt
```

또는 vim 내부에서,

```
diffthis
```

![vimdiff](https://s3.ap-northeast-2.amazonaws.com/jongwony/blog/2018-07-19/vimdiff.gif)


## Option filetype

`syntax on` 과 같이 사용하시면 버퍼에서도 하이라이팅이 가능합니다.

```
:set filetype=python
:set filetype=json
:set filetype=sql
...
```

## Useful glue command

제가 생각하는 가장 강력한 기능입니다.

쉘 커맨드를 자유자재로 실행해서 바로 에디터에 끼워넣을 수 있습니다.

주어진 범위에 해당하는 텍스트를 스크립트로 간주해서 반환된 stdout 또는 stderr 로 치환됩니다.

추가를 할 경우 `read` 명령어를 응용하여 다음과 같이 입력하시면 됩니다.

```
10:r !echo hello world!
```

콜론 앞 숫자는 해당 라인에 작업을 시행하는 것입니다.

여러분도 잘 아실 수도 있는 치환작업은 생각보다 여러방면으로 응용이 가능합니다.
`g` 는 정규식 패턴 매칭이라고 생각하시면 되며, `v` 는 `g!` 와 같습니다.

```
:g/<REGEX>/d
:v/<REGEX>/d
:g/<MATCH>/v/<NOT MATCH>/d
:%s/<REGEX>/<REPLACE>/g

# 모든 매칭한 곳 아래 줄에 hello world!
:g/<REGEX>/normal ohello world!
```

다음은 제 계정의 gist id 를 모두 추출해 내는 예입니다.

<script src="https://asciinema.org/a/192601.js" id="asciicast-192601" async></script>

```
vim
i
curl -s https://api.github.com/users/jongwony/gist
set filetype=json
:v/"id": "/d
:%!cut -f2- -d:
:%s/[",]//g
```

## SQL syntax in Python

IDE 의 역할을 어느정도 대체할 수 있는 강력한 기능이라 생각합니다.

다음 문장을 `~/.vimrc` 에 추가하시고 원하는 파일 타입을 지정해 주시면 됩니다.

저는 python 파일인데 SQL 문법이 인식됩니다.

```vim
" http://vim.wikia.com/wiki/Different_syntax_highlighting_within_regions_of_a_file
au Filetype python call TextEnableCodeSnip('sql', "'''", "'''", 'SpecialComment')

function! TextEnableCodeSnip(filetype,start,end,textSnipHl) abort
    let ft=toupper(a:filetype)
    let group='textGroup'.ft
    if exists('b:current_syntax')
        let s:current_syntax=b:current_syntax
        " Remove current syntax definition, as some syntax files (e.g. cpp.vim)
        " do nothing if b:current_syntax is defined.
        unlet b:current_syntax
    endif
    execute 'syntax include @'.group.' syntax/'.a:filetype.'.vim'
    try
        execute 'syntax include @'.group.' after/syntax/'.a:filetype.'.vim'
    catch
    endtry
    if exists('s:current_syntax')
        let b:current_syntax=s:current_syntax
    else
        unlet b:current_syntax
    endif
    execute 'syntax region textSnip'.ft.'
                \ matchgroup='.a:textSnipHl.'
                \ start="'.a:start.'" end="'.a:end.'"
                \ contains=@'.group
endfunction
```

이렇게 지정함으로써 저는 python filetype(버퍼도 해당)의 멀티라인 `'''` 내의 텍스트는 모두 SQL로 인식하도록 하였습니다.

원하는 언어 및 파일 지정은 위의 `call` 함수만 변형하시면 됩니다.

주석의 홈페이지를 참고하시면 됩니다.

![vim-py-sql-hl](https://s3.ap-northeast-2.amazonaws.com/jongwony/blog/2018-07-19/vim_py_sql_hl.png)

## Join!

- [vimawesome.com](https://vimawesome.com)

다양한 플러그인이 기다리고 있습니다.

- [www.vimgolf.com](http://www.vimgolf.com)

vim 은 직접 해보면서 배우는 것이 중요하다고 생각합니다.

vim 의 명령어들을 익히는 데 매우 효율적인 사이트입니다.

`termux` 안드로이드 애플리케이션과 같이 사용하기에도 괜찮았습니다.
